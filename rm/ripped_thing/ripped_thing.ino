// Sketch for 2 axis Gimbal control using MPU6050 accelerometer/gyro
// 5/8/2014 by Andreas Biekert and Jonah Grubb <andreas.biekert@pomona.edu>
//
// Based directly on sketches by Jeff Rowberg ( https://github.com/jrowberg/i2cdevlib )
// and http://elabz.com/bldc-motor-with-arduino-circuit-and-software/

/* ============================================
I2Cdev device library code is placed under the MIT license
Copyright (c) 2012 Jeff Rowberg

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
===============================================
*/

// I2Cdev and MPU6050 must be installed as libraries, or else the .cpp/.h files
// for both classes must be in the include path of your project
#include "I2Cdev.h"

#include "MPU6050_6Axis_MotionApps20.h"
//#include "MPU6050.h" // not necessary if using MotionApps include file

// Arduino Wire library is required if I2Cdev I2CDEV_ARDUINO_WIRE implementation
// is used in I2Cdev.h
#if I2CDEV_IMPLEMENTATION == I2CDEV_ARDUINO_WIRE
    #include "Wire.h"
#endif

// class default I2C address is 0x68
// specific I2C addresses may be passed as a parameter here
// AD0 low = 0x68 (default for SparkFun breakout and InvenSense evaluation board)
// AD0 high = 0x69
MPU6050 mpu;
//MPU6050 mpu(0x69); // <-- use for AD0 high

/* =========================================================================
   NOTE: In addition to connection 3.3v, GND, SDA, and SCL, this sketch
   depends on the MPU-6050's INT pin being connected to the Arduino's
   external interrupt #0 pin. On the Arduino Uno and Mega 2560, this is
   digital I/O pin 2.
 * ========================================================================= */

/* =========================================================================
   NOTE: Arduino v1.0.1 with the Leonardo board generates a compile error
   when using Serial.write(buf, len). The Teapot output uses this method.
   The solution requires a modification to the Arduino USBAPI.h file, which
   is fortunately simple, but annoying. This will be fixed in the next IDE
   release. For more info, see these links:

   http://arduino.cc/forum/index.php/topic,109987.0.html
   http://code.google.com/p/arduino/issues/detail?id=958
 * ========================================================================= */



// uncomment "OUTPUT_READABLE_QUATERNION" if you want to see the actual
// quaternion components in a [w, x, y, z] format (not best for parsing
// on a remote host such as Processing or something though)
//#define OUTPUT_READABLE_QUATERNION

// uncomment "OUTPUT_READABLE_EULER" if you want to see Euler angles
// (in degrees) calculated from the quaternions coming from the FIFO.
// Note that Euler angles suffer from gimbal lock (for more info, see
// http://en.wikipedia.org/wiki/Gimbal_lock)
//#define OUTPUT_READABLE_EULER

// uncomment "OUTPUT_READABLE_YAWPITCHROLL" if you want to see the yaw/
// pitch/roll angles (in degrees) calculated from the quaternions coming
// from the FIFO. Note this also requires gravity vector calculations.
// Also note that yaw/pitch/roll angles suffer from gimbal lock (for
// more info, see: http://en.wikipedia.org/wiki/Gimbal_lock)
#define OUTPUT_READABLE_YAWPITCHROLL

// uncomment "OUTPUT_READABLE_REALACCEL" if you want to see acceleration
// components with gravity removed. This acceleration reference frame is
// not compensated for orientation, so +X is always +X according to the
// sensor, just without the effects of gravity. If you want acceleration
// compensated for orientation, us OUTPUT_READABLE_WORLDACCEL instead.
//#define OUTPUT_READABLE_REALACCEL

// uncomment "OUTPUT_READABLE_WORLDACCEL" if you want to see acceleration
// components with gravity removed and adjusted for the world frame of
// reference (yaw is relative to initial orientation, since no magnetometer
// is present in this case). Could be quite handy in some cases.
//#define OUTPUT_READABLE_WORLDACCEL

// uncomment "OUTPUT_TEAPOT" if you want output that matches the
// format used for the InvenSense teapot demo
//#define OUTPUT_TEAPOT

// MPU control/status vars
bool dmpReady = false;  // set true if DMP init was successful
uint8_t mpuIntStatus;   // holds actual interrupt status byte from MPU
uint8_t devStatus;      // return status after each device operation (0 = success, !0 = error)
uint16_t packetSize;    // expected DMP packet size (default is 42 bytes)
uint16_t fifoCount;     // count of all bytes currently in FIFO
uint8_t fifoBuffer[64]; // FIFO storage buffer

// orientation/motion vars
Quaternion q;           // [w, x, y, z]         quaternion container
VectorInt16 aa;         // [x, y, z]            accel sensor measurements
VectorInt16 aaReal;     // [x, y, z]            gravity-free accel sensor measurements
VectorInt16 aaWorld;    // [x, y, z]            world-frame accel sensor measurements
VectorFloat gravity;    // [x, y, z]            gravity vector
float euler[3];         // [psi, theta, phi]    Euler angle container
float ypr[3];           // [yaw, pitch, roll]   yaw/pitch/roll container and gravity vector

// Pins to control pitch motor
const int pitchMotor1 = 9;
const int pitchMotor2 = 10;
const int pitchMotor3 = 11;

const int imuInterruptPin = 2;

const int aIn1 = 12;
const int aIn2 = 3;
const int bIn1 = 4;
const int bIn2 = 5;
const int cIn1 = 7;
const int cIn2 = 8;
const int stby = 6;

// Pins to control roll motor
//const int rollMotor1 =3;
//const int rollMotor2 =5;
//const int rollMotor3 =6;

// Variable to let us move motor in desired direction
int increment = 0;

// Controls speed of motor updates
const int motorDelay = 7; // together with pot controls the RPM
long lastMotorDelayTime = 0;


// Variable to intialize orientation of axes
const int pitchOrient = 25;
//const int rollOrient = -20;

const boolean pitch = true;
const boolean roll = false;

//FIXME: More values in this sine?
const int pwmSin[] = {127, 135, 144, 152, 160, 168, 176, 183, 190, 198, 204, 211, 217,
                222, 228, 233, 237, 241, 244, 247, 250, 252, 253, 254, 254, 254,
                253, 252, 250, 247, 244, 241, 237, 233, 228, 222, 217, 211, 204,
                198, 190, 183, 176, 168, 160, 152, 144, 135, 127, 119, 110, 102,
                94, 86, 78, 71, 64, 56, 50, 43, 37, 32, 26, 21, 17, 13, 10, 7,
                4, 2, 1, 0, 0, 0, 1, 2, 4, 7, 10, 13, 17, 21, 26, 32, 37, 43,
                50, 56, 64, 71, 78, 86, 94, 102, 110, 119
              }; 
            // array of PWM duty values for 8-bit timer - sine function}; 

//FIXME: Update with sine values
// Variables for pitch motor
int pitchStepA = 0;
int pitchStepB = 32;
int pitchStepC = 64;

//FIXME: Update with sine values
// Variables for roll motor
//int rollStepA = 0;
//int rollStepB = 32;
//int rollStepC = 64;



// ================================================================
// ===               INTERRUPT DETECTION ROUTINE                ===
// ================================================================

volatile bool mpuInterrupt = false;     // indicates whether MPU interrupt pin has gone high
void dmpDataReady() {
    mpuInterrupt = true;
}



// ================================================================
// ===                      INITIAL SETUP                       ===
// ================================================================

void setup() {
    // join I2C bus (I2Cdev library doesn't do this automatically)
    #if I2CDEV_IMPLEMENTATION == I2CDEV_ARDUINO_WIRE
        Wire.begin();
        TWBR = 24; // 400kHz I2C clock (200kHz if CPU is 8MHz)
    #elif I2CDEV_IMPLEMENTATION == I2CDEV_BUILTIN_FASTWIRE
        Fastwire::setup(400, true);
    #endif

    // initialize serial communication
    // (115200 chosen because it is required for Teapot Demo output, but it's
    // really up to you depending on your project)
    Serial.begin(38400);
    while (!Serial); // wait for Leonardo enumeration, others continue immediately

    // NOTE: 8MHz or slower host processors, like the Teensy @ 3.3v or Ardunio
    // Pro Mini running at 3.3v, cannot handle this baud rate reliably due to
    // the baud timing being too misaligned with processor ticks. You must use
    // 38400 or slower in these cases, or use some kind of external separate
    // crystal solution for the UART timer.

    // initialize device
    Serial.println(F("Initializing I2C devices..."));
    mpu.initialize();

    // verify connection
    Serial.println(F("Testing device connections..."));
    Serial.println(mpu.testConnection() ? F("MPU6050 connection successful") : F("MPU6050 connection failed"));

    // wait for ready
    Serial.println(F("\nSend any character to begin DMP programming and demo: "));
    while (Serial.available() && Serial.read()); // empty buffer
    while (!Serial.available());                 // wait for data
    while (Serial.available() && Serial.read()); // empty buffer again

    // load and configure the DMP
    Serial.println(F("Initializing DMP..."));
    devStatus = mpu.dmpInitialize();

    // supply your own gyro offsets here, scaled for min sensitivity
    mpu.setXGyroOffset(220);
    mpu.setYGyroOffset(76);
    mpu.setZGyroOffset(-85);
    mpu.setZAccelOffset(1788); // 1688 factory default for my test chip

    // make sure it worked (returns 0 if so)
    if (devStatus == 0) {
        // turn on the DMP, now that it's ready
        Serial.println(F("Enabling DMP..."));
        mpu.setDMPEnabled(true);

        // enable Arduino interrupt detection
        Serial.println(F("Enabling interrupt detection (Arduino external interrupt 0)..."));
        attachInterrupt(0, dmpDataReady, RISING);
        mpuIntStatus = mpu.getIntStatus();

        // set our DMP Ready flag so the main loop() function knows it's okay to use it
        Serial.println(F("DMP ready! Waiting for first interrupt..."));
        dmpReady = true;

        // get expected DMP packet size for later comparison
        packetSize = mpu.dmpGetFIFOPacketSize();
    } else {
        // ERROR!
        // 1 = initial memory load failed
        // 2 = DMP configuration updates failed
        // (if it's going to break, usually the code will be 1)
        Serial.print(F("DMP Initialization failed (code "));
        Serial.print(devStatus);
        Serial.println(F(")"));
    }
    
    // Declares the motor pins as output pins
    pinMode(pitchMotor1, OUTPUT); 
    pinMode(pitchMotor2, OUTPUT);
    pinMode(pitchMotor3, OUTPUT);
    pinMode(aIn1, OUTPUT);
    pinMode(aIn2, OUTPUT);
    pinMode(bIn1, OUTPUT);
    pinMode(bIn2, OUTPUT);
    pinMode(cIn1, OUTPUT);
    pinMode(cIn2, OUTPUT);
    pinMode(imuInterruptPin, INPUT);
    
    // Turn everything on
    digitalWrite(aIn1, HIGH);
    digitalWrite(aIn2, LOW);
    digitalWrite(bIn1, HIGH);
    digitalWrite(bIn2, LOW);
    digitalWrite(cIn1, HIGH);
    digitalWrite(cIn2, LOW);
    digitalWrite(stby, HIGH);
  }



// ================================================================
// ===                    MAIN PROGRAM LOOP                     ===
// ================================================================

void loop() {

    // if programming failed, don't try to do anything
    if (!dmpReady) return;

    // wait for MPU interrupt or extra packet(s) available
    while (!mpuInterrupt && fifoCount < packetSize) {

        // Move pitch motor
        if((millis() - lastMotorDelayTime) >  motorDelay) {
          
            move(pitchStepA, pitchStepB, pitchStepC, 
                    pitchMotor1, pitchMotor2, pitchMotor3, pitch);                  
      }
        
        if( mpuInterrupt ) {
          break;
        }
    }

    // reset interrupt flag and get INT_STATUS byte
    
    mpuInterrupt = false;
    mpuIntStatus = mpu.getIntStatus();

    // get current FIFO count
    fifoCount = mpu.getFIFOCount();

    // Serial.println("checking overflow");
    // check for overflow (this should never happen unless our code is too inefficient)
    if ((mpuIntStatus & 0x10) || fifoCount == 1024) {
        
      // reset so we can continue cleanly
        mpu.resetFIFO();
        Serial.println(F("FIFO overflow!"));

    // otherwise, check for DMP data ready interrupt (this should happen frequently)
    } else if (mpuIntStatus & 0x02) {
        // wait for correct available data length, should be a VERY short wait
        while (fifoCount < packetSize) fifoCount = mpu.getFIFOCount();

        // read a packet from FIFO
        mpu.getFIFOBytes(fifoBuffer, packetSize);
        
        // track FIFO count here in case there is > 1 packet available
        // (this lets us immediately read more without waiting for an interrupt)
        fifoCount -= packetSize;

        #ifdef OUTPUT_READABLE_YAWPITCHROLL
            // display Euler angles in degrees
            mpu.dmpGetQuaternion(&q, fifoBuffer);
            mpu.dmpGetGravity(&gravity, &q);
            mpu.dmpGetYawPitchRoll(ypr, &q, &gravity);
            Serial.print("ypr\t");
            Serial.print(ypr[0] * 180/M_PI);
            Serial.print("\t");
            Serial.print(ypr[1] * 180/M_PI);
            Serial.print("\t");
            Serial.println(ypr[2] * 180/M_PI);
        #endif
    }
}

// ================================================================
// ===                   MOTOR MOVE FUNCTION                    ===
// ================================================================
// This function handles the movement of the motor. It takes in
// six arguments, each for a particular motor:
// 
// currentStepA/B/C: references to integers representing the current
//    position in the sine wave for the coils in the motor
// motorPin1/2/3: integers representing the output pins to send
//    motor data to
//
// Updates each of the steps forwards or backwards depending on the
// orientation of the motor vs. the desired value
//
void move(int &currentStepA, int &currentStepB, int &currentStepC, int motorPin1, int motorPin2, int motorPin3, boolean axis)
{
  
  
  // delay time passed, move one step 
  if(  axis == true ) {
    if( (ypr[1] * 180/M_PI) <= pitchOrient ) {
      increment = -1;
    } else {
      increment = 1;
    }
  }
    
  // Keeps motor orientation in bounds
  currentStepA = currentStepA + increment;
  if(currentStepA > 95) currentStepA = 0;
  if(currentStepA < 0) currentStepA = 95;
   
  currentStepB = currentStepB + increment;
  if(currentStepB > 95) currentStepB = 0;
  if(currentStepB < 0) currentStepB = 95;
   
  currentStepC = currentStepC + increment;
  if(currentStepC > 95) currentStepC = 0;
  if(currentStepC < 0) currentStepC = 95;

  // Update motor orientation
  analogWrite(motorPin1, pwmSin[currentStepA]);
  analogWrite(motorPin2, pwmSin[currentStepB]);
  analogWrite(motorPin3, pwmSin[currentStepC]);
  
  lastMotorDelayTime = millis();
  
}

